from emberjson._deserialize.parser import Parser, ParseOptions
from emberjson import Null, Array, Object, Value, from_json
from std.testing import assert_true, assert_equal, assert_raises, TestSuite
from std.memory import bitcast


def test_parse() raises:
    var s: String = '{"key": 123}'
    var p = Parser(s)
    var json = p.parse()
    assert_true(json.is_object())
    assert_equal(json.object()["key"].int(), 123)
    assert_equal(json.object()["key"].int(), 123)

    assert_equal(String(json), '{"key":123}')

    assert_equal(len(json), 1)

    s = "[123, 345]"
    json = from_json[Value](s)
    assert_true(json.is_array())
    assert_equal(json.array()[0].int(), 123)
    assert_equal(json.array()[1].int(), 345)
    assert_equal(json.array()[0].int(), 123)

    assert_equal(String(json), "[123,345]")

    assert_equal(len(json.array()), 2)


def test_parse_utf16_surrogates() raises:
    var s: String = r'{"key": "To decode U+10437 (\uD801\uDC37) from UTF-16:"}'
    var p = Parser(s)
    var json = p.parse()
    assert_true(json.is_object())
    assert_true(json.object()["key"].isa[String]())
    assert_equal(
        json.object()["key"].string(), "To decode U+10437 (𐐷) from UTF-16:"
    )

    var s2: String = r'{"\uD801\uDC37": "\uD801\uDC37"}'
    var p2 = Parser(s2)
    var json2 = p2.parse()
    assert_true(json2.is_object())
    assert_true(json2.object()["𐐷"].isa[String]())
    assert_equal(json2.object()["𐐷"].string(), "𐐷")


def test_parse_escaped_strings() raises:
    # Quotes
    var s_quote = r'{"key": "foo \"bar\""}'
    var json_quote = from_json[Value](s_quote)
    assert_equal(json_quote.object()["key"].string(), 'foo "bar"')

    # Backslash
    var s_bs = r'{"key": "foo \\ bar"}'
    var json_bs = from_json[Value](s_bs)
    assert_equal(json_bs.object()["key"].string(), "foo \\ bar")

    # Forward slash
    var s_fs = r'{"key": "foo \/ bar"}'
    var json_fs = from_json[Value](s_fs)
    assert_equal(json_fs.object()["key"].string(), "foo / bar")

    # Controls
    var s_b = r'{"key": "foo \b bar"}'
    var json_b = from_json[Value](s_b)
    assert_equal(json_b.object()["key"].string(), "foo \b bar")

    var s_f = r'{"key": "foo \f bar"}'
    var json_f = from_json[Value](s_f)
    assert_equal(json_f.object()["key"].string(), "foo \f bar")

    var s_n = r'{"key": "foo \n bar"}'
    var json_n = from_json[Value](s_n)
    assert_equal(json_n.object()["key"].string(), "foo \n bar")

    var s_r = r'{"key": "foo \r bar"}'
    var json_r = from_json[Value](s_r)
    assert_equal(json_r.object()["key"].string(), "foo \r bar")

    var s_t = r'{"key": "foo \t bar"}'
    var json_t = from_json[Value](s_t)
    assert_equal(json_t.object()["key"].string(), "foo \t bar")

    # Null byte \u0000
    var s_null = r'{"key": "foo \u0000 bar"}'
    var json_null = from_json[Value](s_null)
    # Construct expected string with null byte manually
    var expected_null = String("foo ")
    expected_null.append(Codepoint(0))
    expected_null += " bar"
    assert_equal(json_null.object()["key"].string(), expected_null)


def test_parse_wrong_backslash() raises:
    var data = List('{"key": "This should raise and not segfault:'.as_bytes())
    data.append(Byte(ord("\\")))
    with assert_raises():
        var p = Parser(Span(data))
        _ = p.parse()
    data.append(Byte(ord("u")))
    with assert_raises():
        var p = Parser(Span(data))
        _ = p.parse()
    data.extend("D801".as_bytes())
    with assert_raises():
        var p = Parser(Span(data))
        _ = p.parse()
    data.append(Byte(ord("\\")))
    with assert_raises():
        var p = Parser(Span(data))
        _ = p.parse()
    data.append(Byte(ord("u")))
    with assert_raises():
        var p = Parser(Span(data))
        _ = p.parse()
    data.extend("D801".as_bytes())
    with assert_raises():
        var p = Parser(Span(data))
        _ = p.parse()

    var data2 = List('{"key": '.as_bytes())
    data2.append(Byte(ord("\\")))
    data2.extend('"this should not be correct"}'.as_bytes())
    var p2 = Parser(Span(data2))
    with assert_raises():
        _ = p2.parse()


def test_integer_strict_overflow() raises:
    # Int8 max is 127
    var s_ok = "127"
    var p_ok = Parser(s_ok)
    assert_equal(p_ok.expect_int[DType.int8](), 127)

    var s_overflow = "128"
    var p_over = Parser(s_overflow)
    with assert_raises():
        _ = p_over.expect_int[DType.int8]()

    # Int8 min is -128
    var s_min = "-128"
    var p_min = Parser(s_min)
    assert_equal(p_min.expect_int[DType.int8](), -128)

    var s_under = "-129"
    var p_under = Parser(s_under)
    with assert_raises():
        _ = p_under.expect_int[DType.int8]()


def test_unsigned_strict_overflow() raises:
    # UInt8 max is 255
    var s_ok = "255"
    var p_ok = Parser(s_ok)
    assert_equal(p_ok.expect_int[DType.uint8](), 255)

    var s_over = "256"
    var p_over = Parser(s_over)
    with assert_raises():
        _ = p_over.expect_int[DType.uint8]()

    # Negative should fail
    var s_neg = "-5"
    var p_neg = Parser(s_neg)
    with assert_raises():
        _ = p_neg.expect_int[DType.uint8]()


def test_float_conversions() raises:
    # Float32 should parse
    var s = "42.5"
    var p = Parser(s)
    assert_equal(p.expect_float[DType.float32](), 42.5)

    # Int syntax as float
    var s_int = "100"
    var p_int = Parser(s_int)
    assert_equal(p_int.expect_float[DType.float32](), 100.0)

    # Casting check for float might be tricky due to precision,
    # but huge number to float16 might overflow to inf?
    # Float16 max is ~65504
    var s_huge = "100000.0"
    var p_huge = Parser(s_huge)
    with assert_raises():  # Expect overflow for float16
        _ = p_huge.expect_float[DType.float16]()


def test_integer_edge_cases() raises:
    # Leading zeros are invalid (except for just "0")
    var p_01 = Parser("01")
    with assert_raises():
        _ = p_01.expect_int()

    # "0" is valid
    var p_0 = Parser("0")
    assert_equal(p_0.expect_int(), 0)

    # "-0" is valid and should be 0
    var p_neg0 = Parser("-0")
    assert_equal(p_neg0.expect_int(), 0)

    # 64-bit boundaries
    # Int64 Max: 9223372036854775807
    var s_max = "9223372036854775807"
    var p_max = Parser(s_max)
    assert_equal(p_max.expect_int[DType.int64](), 9223372036854775807)

    # Int64 Min: -9223372036854775808
    var s_min = "-9223372036854775808"
    var p_min = Parser(s_min)
    assert_equal(p_min.expect_int[DType.int64](), -9223372036854775808)

    # UInt64 Max: 18446744073709551615
    var s_umax = "18446744073709551615"
    var p_umax = Parser(s_umax)
    assert_equal(p_umax.expect_int[DType.uint64](), 18446744073709551615)

    # 128-bit boundaries
    # Int128 Max
    var s_128_max = "170141183460469231731687303715884105727"
    var p_128_max = Parser(s_128_max)
    assert_equal(p_128_max.expect_int[DType.int128](), Scalar[DType.int128].MAX)

    # Int128 Min
    var s_128_min = "-170141183460469231731687303715884105728"
    var p_128_min = Parser(s_128_min)
    assert_equal(p_128_min.expect_int[DType.int128](), Scalar[DType.int128].MIN)

    # UInt128 Max
    var s_u128_max = "340282366920938463463374607431768211455"
    var p_u128_max = Parser(s_u128_max)
    assert_equal(
        p_u128_max.expect_int[DType.uint128](), Scalar[DType.uint128].MAX
    )

    # 256-bit boundaries
    # Int256 Max
    var s_256_max = "57896044618658097711785492504343953926634992332820282019728792003956564819967"
    var p_256_max = Parser(s_256_max)
    assert_equal(p_256_max.expect_int[DType.int256](), Scalar[DType.int256].MAX)

    # Int256 Min
    var s_256_min = "-57896044618658097711785492504343953926634992332820282019728792003956564819968"
    var p_256_min = Parser(s_256_min)
    assert_equal(p_256_min.expect_int[DType.int256](), Scalar[DType.int256].MIN)

    # UInt256 Max
    var s_u256_max = "115792089237316195423570985008687907853269984665640564039457584007913129639935"
    var p_u256_max = Parser(s_u256_max)
    assert_equal(
        p_u256_max.expect_int[DType.uint256](),
        Scalar[DType.uint256].MAX,
    )

    # Test exact overflow checking correctness (just above Max for uint128)
    var p_u128_over = Parser("340282366920938463463374607431768211456")
    with assert_raises():
        _ = p_u128_over.expect_int[DType.uint128]()

    # Test exact underflow checking correctness (just below Min for int128)
    var p_128_under = Parser("-170141183460469231731687303715884105729")
    with assert_raises():
        _ = p_128_under.expect_int[DType.int128]()


def test_float_edge_cases() raises:
    # Negative zero
    var p_neg0 = Parser("-0.0")
    assert_equal(p_neg0.expect_float(), -0.0)

    # Scientific notation variations
    var p_e1 = Parser("1E1")
    assert_equal(p_e1.expect_float(), 10.0)

    var p_plus = Parser("1e+1")
    assert_equal(p_plus.expect_float(), 10.0)

    var p_minus = Parser("1e-1")
    assert_equal(p_minus.expect_float(), 0.1)

    var p_sci = Parser("1.2e2")
    assert_equal(p_sci.expect_float(), 120.0)

    # Invalid syntax
    with assert_raises():
        var p = Parser("1.")
        _ = p.expect_float()  # Trailing dot

    with assert_raises():
        var p = Parser(".1")
        _ = p.expect_float()  # Leading dot

    with assert_raises():
        var p = Parser("1e")
        _ = p.expect_float()  # Missing exponent

    with assert_raises():
        var p = Parser("1.e1")
        _ = p.expect_float()  # Dot must be followed by digit


def test_unicode_byte_lengths() raises:
    # 1 byte: A (U+0041)
    var s1 = r'{"key": "\u0041"}'
    var j1 = from_json[Value](s1)
    assert_equal(j1.object()["key"].string(), "A")

    # 2 bytes: £ (U+00A3)
    var s2 = r'{"key": "\u00A3"}'
    var j2 = from_json[Value](s2)
    assert_equal(j2.object()["key"].string(), "£")

    # 3 bytes: € (U+20AC)
    var s3 = r'{"key": "\u20AC"}'
    var j3 = from_json[Value](s3)
    assert_equal(j3.object()["key"].string(), "€")

    # 4 bytes: 𝄞 (U+1D11E) - Surrogate pair \uD834\uDD1E
    var s5 = r'{"key": "\uD834\uDD1E"}'
    var j5 = from_json[Value](s5)
    assert_equal(j5.object()["key"].string(), "𝄞")


def test_trailing_tokens() raises:
    with assert_raises(
        contains=(
            "Expected end of input, received trailing content: garbage tokens"
        )
    ):
        _ = from_json[Value]("[1, null, false] garbage tokens")

    with assert_raises(
        contains=(
            "Expected end of input, received trailing content:"
            ' "trailing string"'
        )
    ):
        _ = from_json[Value]('{"key": null} "trailing string"')


def test_incomplete_data() raises:
    with assert_raises():
        _ = from_json[Value]("[1 null, false,")

    with assert_raises():
        _ = from_json[Value]('{"key": 123')

    with assert_raises():
        _ = from_json[Value]('["asdce]')

    with assert_raises():
        _ = from_json[Value]('["no close')


def test_reject_comment() raises:
    var s = """
    {
        // a comment
        "key": 123
    }
"""
    with assert_raises():
        _ = from_json[Value](s)


def test_expect_int_bytes() raises:
    var json = String(
        "12345, -67890, 1234567890123456789, -9876543210987654321"
    )
    var p = Parser(json)
    var span1 = p.expect_int_bytes()
    assert_equal(len(span1), 5)
    # 12345

    p.expect(44)  # ,
    p.skip_whitespace()

    var span2 = p.expect_int_bytes()
    assert_equal(len(span2), 6)
    # -67890

    p.expect(44)  # ,
    p.skip_whitespace()

    var span3 = p.expect_int_bytes()
    assert_equal(len(span3), 19)
    # 1234567890123456789

    p.expect(44)  # ,
    p.skip_whitespace()

    var span4 = p.expect_int_bytes()
    assert_equal(len(span4), 20)
    # -9876543210987654321


def test_expect_float_bytes() raises:
    var json = String(
        "123.45, -6.7e-8, 1.2E+3, 1234567890.123456789e-123,"
        " -0.000000000000000000001"
    )
    var p = Parser(json)

    var span1 = p.expect_float_bytes()
    assert_equal(len(span1), 6)  # 123.45

    p.expect(44)  # ,
    p.skip_whitespace()

    var span2 = p.expect_float_bytes()
    assert_equal(len(span2), 7)  # -6.7e-8

    p.expect(44)  # ,
    p.skip_whitespace()

    var span3 = p.expect_float_bytes()
    assert_equal(len(span3), 6)  # 1.2E+3

    p.expect(44)  # ,
    p.skip_whitespace()

    var span4 = p.expect_float_bytes()
    assert_equal(len(span4), 25)  # 1234567890.123456789e-123

    p.expect(44)  # ,
    p.skip_whitespace()

    var span5 = p.expect_float_bytes()
    assert_equal(len(span5), 24)  # -0.000000000000000000001


def test_expect_value_bytes() raises:
    var json = String(
        '{"a": 1}, [1, 2], "string", 12345, -12.34e5, true, false, null'
    )
    var p = Parser(json)

    # Object
    var span1 = p.expect_value_bytes()
    assert_equal(len(span1), 8)
    assert_equal(StringSlice(unsafe_from_utf8=span1), '{"a": 1}')

    p.expect(44)  # ,

    # Array
    var span2 = p.expect_value_bytes()
    assert_equal(len(span2), 6)
    assert_equal(StringSlice(unsafe_from_utf8=span2), "[1, 2]")

    p.expect(44)  # ,

    # String
    var span3 = p.expect_value_bytes()
    assert_equal(len(span3), 8)
    assert_equal(StringSlice(unsafe_from_utf8=span3), '"string"')

    p.expect(44)  # ,

    # Integer
    var span4 = p.expect_value_bytes()
    assert_equal(len(span4), 5)
    assert_equal(StringSlice(unsafe_from_utf8=span4), "12345")

    p.expect(44)  # ,

    # Float
    var span5 = p.expect_value_bytes()
    assert_equal(len(span5), 8)
    assert_equal(StringSlice(unsafe_from_utf8=span5), "-12.34e5")

    p.expect(44)  # ,

    # True
    var span6 = p.expect_value_bytes()
    assert_equal(len(span6), 4)
    assert_equal(StringSlice(unsafe_from_utf8=span6), "true")

    p.expect(44)  # ,

    # False
    var span7 = p.expect_value_bytes()
    assert_equal(len(span7), 5)
    assert_equal(StringSlice(unsafe_from_utf8=span7), "false")

    p.expect(44)  # ,

    # Null
    var span8 = p.expect_value_bytes()
    assert_equal(len(span8), 4)
    assert_equal(StringSlice(unsafe_from_utf8=span8), "null")

    with assert_raises(contains='Encountered EOF when expecting "true"'):
        var p = Parser("tru")
        _ = p.parse_true()

    with assert_raises(contains='Encountered EOF when expecting "false"'):
        var p = Parser("fals")
        _ = p.parse_false()

    with assert_raises(contains='Encountered EOF when expecting "null"'):
        var p = Parser("nul")
        _ = p.parse_null()


def _f32_bits(s: String) raises -> UInt32:
    return bitcast[DType.uint32](from_json[Float32](s))


def _f16_bits(s: String) raises -> UInt16:
    return bitcast[DType.uint16](from_json[Float16](s))


def test_float32_subnormal_midpoints() raises:
    # Each input's float64 rounding lands exactly on a float32 midpoint;
    # the decimal itself sits just above (round up) or below (round down).
    assert_equal(_f32_bits("7.0064923216240854e-46"), 0x1)
    assert_equal(_f32_bits("7.0064923216240850e-46"), 0x0)
    assert_equal(_f32_bits("-7.0064923216240854e-46"), 0x80000001)
    assert_equal(_f32_bits("2.1019476964872257e-45"), 0x2)
    assert_equal(_f32_bits("2.1019476964872255e-45"), 0x1)
    assert_equal(_f32_bits("1.1754942807573643e-38"), 0x800000)
    assert_equal(_f32_bits("1.17549428075736423e-38"), 0x7FFFFF)
    # Zeros must stay on the fast path and keep their sign.
    assert_equal(_f32_bits("0.0"), 0x0)
    assert_equal(_f32_bits("-0.0"), 0x80000000)
    # Deep underflow (far below the smallest subnormal) through the same
    # slow-path guard must still land on signed zero.
    assert_equal(_f32_bits("-1e-50"), 0x80000000)


def test_float16_subnormal_midpoints() raises:
    assert_equal(_f16_bits("2.9802322387695313e-08"), 0x1)
    assert_equal(_f16_bits("2.9802322387695311e-08"), 0x0)
    assert_equal(_f16_bits("8.9406967163085938e-08"), 0x2)
    assert_equal(_f16_bits("8.9406967163085931e-08"), 0x1)
    assert_equal(_f16_bits("6.1005353927612305e-05"), 0x400)
    assert_equal(_f16_bits("6.1005353927612302e-05"), 0x3FF)
    # Deep underflow (far below the smallest subnormal) through the same
    # slow-path guard must still land on signed zero.
    assert_equal(_f16_bits("-1e-10"), 0x8000)


def test_float_overflow_boundary_midpoints() raises:
    # Mirror of the subnormal midpoint tests at the other end of the range:
    # the float64 intermediate for these decimals lands exactly on the
    # float32/float16 FLT_MAX/infinity midpoint, so a plain cast
    # double-rounds to infinity even though the correctly-rounded decimal
    # result is finite (FLT_MAX / float16 max). Verified with exact
    # rational arithmetic: FLT_MAX = (2 - 2^-23)*2^127, midpoint to the
    # next (unrepresentable) value is 2^128 - 2^103; float16 max is 65504,
    # its midpoint is 65520.
    assert_equal(_f32_bits("3.4028235677973366e38"), 0x7F7FFFFF)
    assert_equal(_f16_bits("65519.99999999999999"), 0x7BFF)

    # Just above the float32 midpoint: the exact decimal is greater than
    # the midpoint, so it must still raise (correctly rounds up to inf).
    with assert_raises():
        _ = _f32_bits("3.4028235677973367e38")

    # Clearly too large for either target: still raises.
    with assert_raises():
        _ = _f32_bits("3.5e38")
    with assert_raises():
        _ = _f16_bits("65520.0001")


def test_all_zero_mantissa_at_end_of_input() raises:
    # More than 19 mantissa characters takes the `significant_digits` path;
    # an all-zero mantissa used to be scanned past the end of the input.
    # The buffer is allocated at exactly the input's size so that, under
    # ASAN, a read past it is a heap-buffer-overflow.
    for text in ["0.00000000000000000000", "-0.000000000000000000000"]:
        var buf = List[Byte](capacity=text.byte_length())
        for b in text.as_bytes():
            buf.append(b)
        var p = Parser(Span(buf))
        assert_equal(p.expect_float(), 0.0)


def main() raises:
    TestSuite.discover_tests[__functions_in_module()]().run()
